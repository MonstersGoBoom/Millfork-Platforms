//	build 
//	millfork -t mega65_small -i . m65_fire.mfk

import stdio
import c64_kernal
import c64_sid
import c64_vic
import random

array m65_screen[80*25] @$800
array(byte) m65_colors[80*25] @$d800

const byte delayTime = $06;

const array(byte) paletter = [ $00, $02, $06, $08, $0F, $0F, $0F, $E0, $02, $04, $06, $08, $0A, $0C, $0E, $0F ]
const array(byte) paletteg = [ $00, $40, $40, $80, $0A, $0F, $0F, $E0, $02, $04, $06, $08, $0A, $0C, $0E, $0F ]
const array(byte) paletteb = [ $00, $00, $00, $40, $40, $60, $0F, $E0, $02, $04, $06, $08, $0A, $0C, $0E, $0F ]

const array(byte) firecolours = [ $00, $01, $02, $03, $04, $05, $06, $06 ]
const array(byte) firenewvalue = [ $02, $03, $04, $05, $05, $06, $06, $07 ]

array(byte) fireblock[440]
byte c
byte d
byte i
byte x
byte y
word p
word q

void ClearScreen(byte Character)
{
	for p,0,until,2000
	{
		m65_screen[p] = Character
	}
}

void SetColourRam(byte Colour)
{
	for p,0,until,2000
	{
		m65_colors[p] = Colour
	}
}

void FrameIRQ()
{
	vic_rasterirq_acknowledge()
	vic_border = $10

	d = d - 1
	if( d == 0) 
	{
		d = delayTime

		// Create new fire line
		for p,0,until,40
		{
			fireblock[400 + p] = rand() & $07
		}

		// Calculate new flames
		for y,0,until,10
		{
			p = word(y) * word(40)
			q = p + 40
			for x,0,until,40
			{
				c = fireblock[q]
				i = 1
				if( x > 0 )
				{
					c = c + fireblock[q - 1]
					i = i + 1
				}
				if( x < 39 )
				{
					c = c + fireblock[q + 1]
					i = i + 1
				}
				c = c / i

				if( rand() & $01 == $01 )
				{
					if(c > 0)
					{
						c = c -1
					}
				}
				fireblock[p] = c
				p = p + 1
				q = q + 1
			}
		}

		// Draw Fire
		for y,0,until,11
		{
			p = word(y) * word(40)
			q = word(y + 14) * word(40)
			for x,0,until,40
			{
				c = fireblock[p]
				m65_colors[q] = firecolours[c]
				p = p + 1
				q = q + 1
			}
		}


	}

	vic_border = $00
	vic_rasterirq_return()
}

void main() 
{
	// Initialize MEGA65
	// asm { sei }
	M65_40hz()
	M65_defaultmem()
	M65_VIC4Enable()
	vic4_40_column_mode()

	// Configure Screen
	vic_border = 0
	vic_bg_color0 = 0
	ClearScreen( $A0 )
	SetColourRam( 0 )

	// Set colour palette
	for c,0,paralleluntil,16
	{
		vic4_red_channel[c] = paletter[c]
		vic4_green_channel[c] = paletteg[c]
		vic4_blue_channel[c] = paletteb[c]
	}
	
	for c,0,until,440
	{
		fireblock[c] = 0
	}

	d = delayTime

	// Raster IRQ at top of screen
	vic_rasterirq_configure(FrameIRQ.addr, 0)

	// Loop
	while true
	{
	}
}
